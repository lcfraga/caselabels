/**
 * Returns the address of the docker host running containerName.
 *
 */
def getHostAddress(containerName) {
    return sh(
        script: "script/docker/compose-ci exec -T ${containerName} /sbin/ip route | awk '/default/ { print \$3 }'",
        returnStdout: true
    ).trim()
}

/**
 * Returns the port exposed by containerName(:containerPort).
 *
 */
def getExposedPort(containerName, containerPort) {
    return sh(
            script: "script/docker/compose-ci port ${containerName} ${containerPort} | cut -d ':' -f 2",
            returnStdout: true
    ).trim()
}

pipeline {
    agent none

    options {
        buildDiscarder(logRotator(numToKeepStr:'5'))
        timeout(time: 15, unit: 'MINUTES')
    }

    stages {
        stage('Install dependencies') {
            agent {
                docker {
                    image 'node:lts-alpine'
                }
            }

            steps {
                dir('backend') {
                    sh 'npm ci'
                }
            }
        }

        stage('Run linter') {
            agent {
                docker {
                    image 'node:lts-alpine'
                }
            }

            steps {
                dir('backend') {
                    sh 'npm run lint'
                }
            }
        }

        stage('Run unit tests') {
            agent {
                docker {
                    image 'node:lts-alpine'
                }
            }

            environment {
                MOCHA_REPORTER    = 'mocha-jenkins-reporter'
                JUNIT_REPORT_PATH = 'report.xml'
                ENABLE_DB         = 'false'
                JWT_PUBLIC_KEY    = 'jwt-public-key-placeholder'
                JWT_PRIVATE_KEY   = 'jwt-private-key-placeholder'
            }

            steps {
                dir('backend') {
                    sh "npm install ${MOCHA_REPORTER}"
                    sh 'npm test'
                }
            }

            post {
                always {
                    junit "backend/${JUNIT_REPORT_PATH}"
                }
            }
        }

        stage('Build backend docker image') {
            agent { label 'master' }

            steps {
                dir('backend') {
                    sh 'script/dockerize'
                }
            }
        }

        stage('Start mongo and backend containers') {
            agent { label 'master' }

            steps {
                sh 'script/docker/compose-ci up -d'
                sleep 15

                // We'll need these in the system test stage.
                sh "echo ${getHostAddress('backend')} > system-tests-host"
                sh "echo ${getExposedPort('backend', 3000)} > system-tests-port"
            }
        }

        stage('Run system tests') {
            agent {
                docker {
                    image 'openjdk:11-jdk-slim'
                }
            }

            environment {
                HOST = sh(script: 'cat system-tests-host', returnStdout: true).trim()
                PORT = sh(script: 'cat system-tests-port', returnStdout: true).trim()
            }

            steps {
                dir('backend/test/system') {
                    sh './gradlew --info clean'
                    sh './gradlew --stop' // The tests will hang if we don't stop the current daemon.
                    sh './gradlew --info test'
                }
            }

            post {
                always {
                    junit 'backend/test/system/build/test-results/test/TEST-*.xml'
                    sh 'rm -f system-tests-host system-tests-port'
                }
            }
        }
    }

    post {
        always {
            node('built-in') {
                sh 'script/docker/compose-ci down --volumes'
                sh 'docker system prune --volumes -f'
            }
        }
    }
}
